// Enemy by Imagine Dragons

// Doubles each entry in a song.
// e.g., A4 -> B4 becomes
// A4 -> A4 -> B4 -> B4
FUNCTION DOUBLE(x) = x -> x;

r = DOUBLE((D5, 1/3)) -> DOUBLE((D5, 1/6)) -> B4;
s = TRANSPOSE(r, -1);

t = (B5, 1/2) -> PAUSE(1/2) -> (A5, 1/2) ->
    PAUSE(1/2) -> (G5, 1/2) -> PAUSE(1/2) ->
    (D5, 1/2) -> (F#5, 2);

// For each step `y` in `t`, produces
// the union of `y` and its transposed version.
// e.g., if t was A4 -> B4, the result would
// be (A4 + TRANSPOSE(A4, -5)) ->
// (B4 + TRANSPOSE(B4, -5)).
t = MAP y IN t => y + TRANSPOSE(y, -5);

u = [(B4, 1/2) , (F#4, 1/2), (B3, 1/2)] ->
    (E5, 3/4) -> (F#5, 1/4) -> (E5, 1/4) ->
    (D5, 1/4) -> B4 -> PAUSE(1/2);

v1 = REPEAT((E5, 1/4) -> (D5, 1/4), 5) -> (B5, 3/2);

v2 = REPEAT((E5, 1/4) -> (D5, 1/4), 3) -> B5;

w = REPEAT((B4, 1/4), 3) -> (D3, 3/4);

intro = DOUBLE(DOUBLE(r) -> DOUBLE(s));
prerit = t -> (E5, 2) -> (B5, 1/2) -> t -> (A#5,2);
rit = u -> v1 -> u -> v2 -> w;

EXPORT CHANGETIME(intro -> prerit -> rit, 5/6) TO "example_10.wav";